

/INFILE=placeholder.in # Input (source) section
	/PROCESS=RANDOM
	/SPEC=..\metadata\report.ddf
	/INCLUDE WHERE CUSTOMER < "Abad"
	/INCOLLECT=50

/SORT # Process (transform) section
	/KEY=(CUSTOMER)
	/KEY=(EMPLOYEE)

/OUTFILE=report.csv # Output (target) section
/FIELD=(SKU, TYPE=WHOLE_NUMBER, POSITION=1, SIZE=6, FILL='0', SEPARATOR=",")
/FIELD=(CUSTOMER, TYPE=ASCII, POSITION=2, SEPARATOR=",", FRAME=',')
/FIELD=(EMPLOYEE, TYPE=UPPER, POSITION=3, SEPARATOR=",", FRAME='"')
/FIELD=(SHIP_DATE, TYPE=AMERICAN_DATE, POSITION=4, SEPARATOR=",")
/FIELD=(FREIGHT, TYPE=NUMERIC, POSITION=5, SEPARATOR=",", SIZE=3, PRECISION=0)


/OUTFILE=report.sum # Custom summary report 
	/PROCESS=FIXED
	/HEADREC="Customer                            Max      Min    Avg  Ct\n------------------------------------------------------------------------------\n"
	/FIELD=(CUSTOMER, TYPE=ASCII, POSITION=1)
	/FIELD=(MAX_FREIGHT, TYPE=NUMERIC, POSITION=35, SIZE=6, PRECISION=2)
	/FIELD=(MIN_FREIGHT, TYPE=NUMERIC, POSITION=43, SIZE=6, PRECISION=2)
	/FIELD=(AVG_FREIGHT, TYPE=NUMERIC, POSITION=50, SIZE=6, PRECISION=2)
	/FIELD=(CT_FREIGHT, TYPE=NUMERIC, POSITION=58)
	/MAXIMUM MAX_FREIGHT FROM FREIGHT BREAK CUSTOMER
	/MINIMUM MIN_FREIGHT FROM FREIGHT BREAK CUSTOMER
	/AVERAGE AVG_FREIGHT FROM FREIGHT BREAK CUSTOMER
	/COUNT CT_FREIGHT BREAK CUSTOMER

/OUTFILE=report.html # Web report detail format
	/PROCESS=FIXED
	/HEADREC="<HTML><HEAD>\n<TITLE>HTML produced by IRI 4GL (SortCL) Program in RowGen </TITLE>\n</HEAD>\n<BODY><H2>Customer Freight Summary</H2><FONT COLOR='RED'>Customer transactions, by SKU; charges above \$500 are shown in green.</FONT>\n\r<TABLE CELLPADDING=4 CELLSPACING=1 BORDER COLS=5><TR><TH>Customer</TH><TH>SKU</TH><TH>Freight</TH></TR>"
	/DATA="<TR><TD>"
	/FIELD=(CUSTOMER, TYPE=ASCII, POSITION=1)
	/DATA="</TD>\n<TD>"
	/FIELD=(SKU, TYPE=WHOLE_NUMBER, POSITION=2, SIZE=6, FILL='0')
	/DATA="</B></TD>\n<TD align=right>"
	/DATA=IF FREIGHT GT 500 THEN "<FONT COLOR='GREEN'>"
	/FIELD=(FREIGHT, TYPE=CURRENCY, POSITION=3, SIZE=15)
	/DATA=IF FREIGHT GT 500 THEN "</FONT>"

/OUTFILE=report.html # Web report summary format
	/PROCESS=FIXED
	/DATA="<TR></TD>\n<TD><B><FONT COLOR='BLUE'>"
	/DATA="Grand Total for "
	/FIELD=(CUSTOMER, TYPE=ASCII, POSITION=1)
	/DATA="<TD>"
	/DATA=""
	/DATA="</FONT></B></TD>\n<TD align=right><B><U><FONT SIZE=+2><FONT COLOR='BLUE'>"
	/FIELD=(TOT_FREIGHT, TYPE=CURRENCY, POSITION=2, SIZE=15)
	/SUM TOT_FREIGHT FROM FREIGHT BREAK CUSTOMER
	/FOOTREC="</TABLE><BR>\nBy <B>%s</B> as of %s.<HR></BODY>\n</HTML>", USER, AMERICAN_TIMESTAMP
