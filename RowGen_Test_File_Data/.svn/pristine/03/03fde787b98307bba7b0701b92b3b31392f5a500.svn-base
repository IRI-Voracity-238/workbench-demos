/INFILE=placeholder.in				# Input (source) section
	/PROCESS=RANDOM						
	/SPEC=..\metadata\report.ddf
	/INCLUDE WHERE CUSTOMER < "Abad"
	/INCOLLECT=50

/SORT								# Process (transform) section
    /KEY=(CUSTOMER)
    /KEY=(EMPLOYEE)
    
    
/OUTFILE=report.csv					# Output (target) section

/OUTFILE=report.sum     	# Custom summary report 
    /HEADREC="Customer                            Max      Min    Avg  Ct\n------------------------------------------------------------------------------\n"
     /FIELD=(CUSTOMER, POSITION=1)
     /FIELD=(max_FREIGHT, POSITION=35,SIZE=6, PRECISION=2)
     /FIELD=(min_FREIGHT, POSITION=43,SIZE=6, PRECISION=2)
     /FIELD=(avg_FREIGHT, POSITION=50,SIZE=6, PRECISION=2)
     /FIELD=(ct_FREIGHT, POSITION=58)
     /MAXIMUM max_FREIGHT FROM FREIGHT BREAK CUSTOMER
     /MINIMUM min_FREIGHT FROM FREIGHT BREAK CUSTOMER
     /AVERAGE avg_FREIGHT FROM FREIGHT BREAK CUSTOMER
     /COUNT ct_FREIGHT BREAK CUSTOMER
     
          
/OUTFILE=report.html   	# Web report detail format
    /HEADREC="<HTML><HEAD>\n<TITLE>HTML produced by IRI 4GL (SortCL) Program in RowGen </TITLE>\n</HEAD>\n<BODY><H2>Customer Freight Summary</H2><FONT COLOR='RED'>Customer transactions, by SKU; charges above \$500 are shown in green.</FONT>\n\r<TABLE CELLPADDING=4 CELLSPACING=1 BORDER COLS=5><TR><TH>Customer</TH><TH>SKU</TH><TH>Freight</TH></TR>"
    /DATA="<TR><TD>"
    /FIELD=(CUSTOMER)
    /DATA="</TD>\n<TD>"
    /FIELD=(SKU, TYPE=WHOLE_NUMBER, SIZE=6, FILL='0')
    /DATA="</B></TD>\n<TD align=right>"
    /DATA=IF FREIGHT GT 500 THEN "<FONT COLOR='GREEN'>"
    /FIELD=(FREIGHT, SIZE=15, TYPE=currency)
    /DATA=IF FREIGHT GT 500 THEN "</FONT>"

/OUTFILE=report.html   	# Web report summary format
    /DATA="<TR></TD>\n<TD><B><FONT COLOR='BLUE'>"
    /DATA="Grand Total for "
    /FIELD=(CUSTOMER)
    /DATA="<TD>"                                                                
    /DATA=""
    /DATA="</FONT></B></TD>\n<TD align=right><B><U><FONT SIZE=+2><FONT COLOR='BLUE'>"
    /FIELD=(tot_FREIGHT, SIZE=15, TYPE=currency)
    /SUM tot_FREIGHT FROM FREIGHT BREAK CUSTOMER
   /FOOTREC="</TABLE><BR>\nBy <B>%s</B> as of %s.<HR></BODY>\n</HTML>",USER,AMERICAN_TIMESTAMP
