# CoSort Sort Control Language (SortCL) Program
# 	Change Data Capture (CDC) Application
# Copyright 2013 Innovative Routines International (IRI), Inc.
#

#################################
# Input (Source Definition) Phase
#################################

/INFILE=sources/previous.txt # unsorted
	/PROCESS=DELIMITED
	/ALIAS=previous
	/FIELD=(DEBT_ID, TYPE=ASCII, POSITION=1, SEPARATOR=",")
	/FIELD=(CHECKSUM, TYPE=ASCII, POSITION=2, SEPARATOR=",")
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=3, PRECISION=2, SEPARATOR=",")

/INFILE=sources/current.txt # pre-sorted over DEBT_ID
	/PROCESS=RECORD
	/ALIAS=current
	/FIELD=(DEBT_ID, TYPE=ASCII, POSITION=1, SIZE=12)
	/FIELD=(CONSUMER, TYPE=ASCII, POSITION=17, SIZE=10)
	/FIELD=(CHECKSUM, TYPE=ASCII, POSITION=33, SIZE=6)
	/FIELD=(TRANSDATE, TYPE=ISO_DATE, POSITION=41, SIZE=10)
	/FIELD=(AMOUNT, TYPE=NUMERIC, POSITION=54, SIZE=7, PRECISION=2)
	/FIELD=(DATE_DUE, TYPE=ISO_DATE, POSITION=63, SIZE=10)
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=76, SIZE=7, PRECISION=2)

	/CONDITION=(PREV_EXIST, TEST=(PREVIOUS.CHECKSUM NE ""))
	/CONDITION=(PREV_NONEXIST, TEST=(isempty(PREVIOUS.CHECKSUM)))
	/CONDITION=(CURR_EXIST, TEST=(CURRENT.CHECKSUM NE "      "))
	/CONDITION=(CURR_NONEXIST, TEST=(isempty(CURRENT.CHECKSUM)))
	/CONDITION=(UPDATE, TEST=(PREVIOUS.CHECKSUM NE CURRENT.CHECKSUM)) 
	
	
#################################
#Action (Matching Process) Phase
#################################

/JOIN FULL_OUTER NOT_SORTED previous current WHERE PREVIOUS.DEBT_ID EQ CURRENT.DEBT_ID # Business Logic for Target Reports



#################################
# Output (Target/Reporting) Phase
#################################

/OUTFILE=targets/TestDeltas.out # Layout
	/PROCESS=RECORD
	/HEADREC="Prev: DebtID    Cksum       Curr: DebtID   Consumer    Cksum   TransDate      Amount  DueDate        Paid    Delta\n\r"
	/FIELD=(PREVIOUS.DEBT_ID, TYPE=ASCII, POSITION=1, SIZE=13)
	/FIELD=(PREVIOUS.CHECKSUM, TYPE=ASCII, POSITION=17, SIZE=6)
	/FIELD=(CURRENT.DEBT_ID, TYPE=ASCII, POSITION=29, SIZE=13)
	/FIELD=(CURRENT.CONSUMER, TYPE=ASCII, POSITION=44, SIZE=10)
	/FIELD=(CURRENT.CHECKSUM, TYPE=ASCII, POSITION=56, SIZE=6)
	/FIELD=(TRANSDATE, POSITION=64, SIZE=10, TYPE=ISO_DATE)
	/FIELD=(AMOUNT, TYPE=NUMERIC, POSITION=76, SIZE=9, PRECISION=2)
	/FIELD=(DATE_DUE, TYPE=ISO_DATE, POSITION=87, SIZE=10)
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=98, SIZE=10, PRECISION=2)
	/FIELD=(DELTA_FLAG,TYPE=ASCII, POSITION=110, SIZE=10, IF UPDATE AND PREV_EXIST AND CURR_EXIST THEN "Update" ELSE IF PREV_EXIST AND CURR_NONEXIST THEN "Delete" ELSE IF PREV_NONEXIST AND CURR_EXIST THEN "Insert" ELSE "Equal")
# Applying the Condition to Report on the Change Status


/OUTFILE=targets/Updates.out
	/PROCESS=RECORD
	/INCLUDE WHERE UPDATE AND PREV_EXIST AND CURR_EXIST # Conditional Selection
	/HEADREC="Prev: DebtID    Cksum       Curr: DebtID   Consumer    Cksum   TransDate      Amount  DueDate        Paid    Delta\n\r"
	/FIELD=(PREVIOUS.DEBT_ID, TYPE=ASCII, POSITION=1, SIZE=13)
	/FIELD=(PREVIOUS.CHECKSUM, TYPE=ASCII, POSITION=17, SIZE=6)
	/FIELD=(CURRENT.DEBT_ID, TYPE=ASCII, POSITION=29, SIZE=13)
	/FIELD=(CURRENT.CONSUMER, TYPE=ASCII, POSITION=44, SIZE=10)
	/FIELD=(CURRENT.CHECKSUM, TYPE=ASCII, POSITION=56, SIZE=6)
	/FIELD=(TRANSDATE, POSITION=64, SIZE=10, TYPE=ISO_DATE)
	/FIELD=(AMOUNT, TYPE=NUMERIC, POSITION=76, SIZE=9, PRECISION=2)
	/FIELD=(DATE_DUE, TYPE=ISO_DATE, POSITION=87, SIZE=10)
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=98, SIZE=10, PRECISION=2)
	/DATA="  Update"

/OUTFILE=targets/Deletes.out
	/PROCESS=RECORD
	/INCLUDE WHERE PREV_EXIST AND CURR_NONEXIST # Conditional Selection
	/HEADREC="Prev: DebtID    Cksum       Curr: DebtID   Consumer    Cksum   TransDate      Amount  DueDate        Paid    Delta\n\r"
	/FIELD=(PREVIOUS.DEBT_ID, TYPE=ASCII, POSITION=1, SIZE=13)
	/FIELD=(PREVIOUS.CHECKSUM, TYPE=ASCII, POSITION=17, SIZE=6)
	/FIELD=(CURRENT.DEBT_ID, TYPE=ASCII, POSITION=29, SIZE=13)
	/FIELD=(CURRENT.CONSUMER, TYPE=ASCII, POSITION=44, SIZE=10)
	/FIELD=(CURRENT.CHECKSUM, TYPE=ASCII, POSITION=56, SIZE=6)
	/FIELD=(TRANSDATE, POSITION=64, SIZE=10, TYPE=ISO_DATE)
	/FIELD=(AMOUNT, TYPE=NUMERIC, POSITION=76, SIZE=9, PRECISION=2)
	/FIELD=(DATE_DUE, TYPE=ISO_DATE, POSITION=87, SIZE=10)
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=98, SIZE=10, PRECISION=2)
	/DATA="  Delete"

/OUTFILE=targets/Inserts.out
	/PROCESS=RECORD
	/INCLUDE WHERE PREV_NONEXIST AND CURR_EXIST # Conditional Selection
	/HEADREC="Prev: DebtID    Cksum       Curr: DebtID   Consumer    Cksum   TransDate      Amount  DueDate        Paid    Delta\n\r"
	/FIELD=(PREVIOUS.DEBT_ID, TYPE=ASCII, POSITION=1, SIZE=13)
	/FIELD=(PREVIOUS.CHECKSUM, TYPE=ASCII, POSITION=17, SIZE=6)
	/FIELD=(CURRENT.DEBT_ID, TYPE=ASCII, POSITION=29, SIZE=13)
	/FIELD=(CURRENT.CONSUMER, TYPE=ASCII, POSITION=44, SIZE=10)
	/FIELD=(CURRENT.CHECKSUM, TYPE=ASCII, POSITION=56, SIZE=6)
	/FIELD=(TRANSDATE, POSITION=64, SIZE=10, TYPE=ISO_DATE)
	/FIELD=(AMOUNT, TYPE=NUMERIC, POSITION=76, SIZE=9, PRECISION=2)
	/FIELD=(DATE_DUE, TYPE=ISO_DATE, POSITION=87, SIZE=10)
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=98, SIZE=10, PRECISION=2)
	/DATA="  Insert"

/OUTFILE=targets/Equals.out
	/PROCESS=RECORD
	/OMIT WHERE UPDATE AND PREV_EXIST AND CURR_EXIST # Conditional selection
	/OMIT WHERE PREV_EXIST AND CURR_NONEXIST
	/OMIT WHERE PREV_NONEXIST AND CURR_EXIST # Layout
	/HEADREC="Prev: DebtID    Cksum       Curr: DebtID   Consumer    Cksum   TransDate      Amount  DueDate        Paid    Delta\n\r"
	/FIELD=(PREVIOUS.DEBT_ID, TYPE=ASCII, POSITION=1, SIZE=13)
	/FIELD=(PREVIOUS.CHECKSUM, TYPE=ASCII, POSITION=17, SIZE=6)
	/FIELD=(CURRENT.DEBT_ID, TYPE=ASCII, POSITION=29, SIZE=13)
	/FIELD=(CURRENT.CONSUMER, TYPE=ASCII, POSITION=44, SIZE=10)
	/FIELD=(CURRENT.CHECKSUM, TYPE=ASCII, POSITION=56, SIZE=6)
	/FIELD=(TRANSDATE, TYPE=ISO_DATE, POSITION=64, SIZE=10)
	/FIELD=(AMOUNT, TYPE=NUMERIC, POSITION=76, SIZE=9, PRECISION=2)
	/FIELD=(DATE_DUE, TYPE=ISO_DATE, POSITION=87, SIZE=10)
	/FIELD=(PAID, TYPE=NUMERIC, POSITION=98, SIZE=10, PRECISION=2)
	/DATA="  Equal"
	